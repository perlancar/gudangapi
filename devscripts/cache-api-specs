#!/usr/bin/perl -w

use 5.010;
use autodie;
use strict;
use warnings;
use FindBin '$Bin';
use lib "$Bin/../lib/perl";
BEGIN { no warnings; $main::LOG_LEVEL = 'info' }
use Log::Any::App '$log';

#use Data::Dumper;
use Data::Dump qw(pp);

use File::Slurp;
use Getopt::Long;
use Module::List qw(list_modules);

#$Data::Dumper::Indent   = 0;
#$Data::Dumper::Terse    = 1;
#$Data::Dumper::Sortkeys = 1;

my $modules = list_modules("GudangAPI::API::", {list_modules=>1, recurse=>1});
our %api_specs;

my %cmdopts;
GetOptions(
    'dry-run|d' => \$cmdopts{dry_run},
);

my $n_modules = 0;
my $n_funcs   = 0;
$log->info("Loading Perl modules ...");
for my $m0 (sort { length($a)<=>length($b) || $a cmp $b }
                keys %$modules) {
    $log->info("Loading $m0 ...");
    eval "use $m0";
    if ($@) {
        $log->error("Can't require $m0, skipped. Error=$@");
        next;
    }

    $n_modules++;
    my $m = $m0; $m =~ s/^GudangAPI::API:://;

    $api_specs{$m} = {functions=>{}};

    my $subs;
    eval "\$subs = \\%$m0\::SPEC";
    if ($@) {
        $log->error("Can't load \%SPEC from $m0, skipped.");
    }
    $api_specs{$m}{functions} = {};
    for my $f (keys %$subs) {
        $n_funcs++;
        $subs->{$f}{_module}   = $m;
        $subs->{$f}{name}    //= $f;
        $api_specs{$m}{functions}{$f} = {
            spec   =>$subs->{$f},
        };
    }
}

$log->infof("Number of API modules: %d. Number of API functions: %d.",
            $n_modules,
            $n_funcs
        );

exit 0 if $cmdopts{dry_run};

my $common_header = join(
    "",
    "# Generated by: $0 ", join(" ", @ARGV), "\n",
    "# Date: ", scalar(localtime), "\n",
);
my $common_pm_header = join(
    "",
    "package GudangAPI::App;\n",
    "\n",
    "use 5.0.10;\n",
    "our \%api_specs;\n",
    "\n",
    $common_header,
);
my $fh;

my $path = "$Bin/../lib/perl/GudangAPI/App/APIFuncs.pm";
$log->info("Writing $path ...");
open $fh, ">", $path;
print $fh $common_pm_header;
print $fh "# List all API functions from all GudangAPI::API::* modules\n";
print $fh "\n";
for my $m (keys %api_specs) {
    print $fh "\$api_specs{'$m'} //= { functions=>{} };\n";
    for my $f (keys %{ $api_specs{$m}{functions} }) {
        $log->trace("Function: $m $f");
        print $fh ("\$api_specs{'$m'}{functions}{'$f'}",
                   " //= {summary=>q(",
                   $api_specs{$m}{functions}{$f}{spec}{summary},
                   ")};\n");
    }
}
print $fh "1;\n";
close $fh;

$path = "$Bin/../lib/perl/GudangAPI/App/APISpecs.pm";
$log->info("Writing $path ...");
open $fh, ">", $path;
print $fh $common_pm_header;
print $fh "# List all API specs from all GudangAPI::API::* modules\n";
print $fh "\n";
#print $fh "my \$api_specs = ", Dumper(\%api_specs), ";\n";
#print $fh "\%api_specs = \%\$api_specs;\n";
print $fh "\%api_specs = ", pp(%api_specs), ";\n";
print $fh "1;\n";
close $fh;
